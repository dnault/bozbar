buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'provided-base'
apply plugin: 'findbugs'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'


repositories {
    jcenter()
}

group = "com.github.therapi"
version = "0.2.0-SNAPSHOT"

def githubProjectName = "therapi-json-rpc"
def projectDescription = "JSON-RPC Microframework for Java"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    def slf4jVersion = '1.7.19'
    def jacksonVersion = '2.7.3'

    provided 'javax.servlet:javax.servlet-api:3.1.0'

    // optional dependency
    provided 'org.springframework:spring-web:4.2.5.RELEASE'

    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.guava:guava:19.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'aopalliance:aopalliance:1.0'

    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.module:jackson-module-parameter-names:${jacksonVersion}"
    compile "com.fasterxml.jackson.module:jackson-module-jsonSchema:${jacksonVersion}"

    compile "com.github.therapi:therapi-runtime-javadoc:0.1.2"

    testCompile 'junit:junit:4.11'
    testCompile 'net.javacrumbs.json-unit:json-unit:1.6.0'
    testCompile "org.slf4j:slf4j-simple:${slf4jVersion}"
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}


def gitUrl = "git@github.com:dnault/${githubProjectName}.git";

def pomConfig = {
    name "${group}:${project.name}"
    description projectDescription
    url "https://github.com/dnault/${githubProjectName}"
    scm {
        url "${gitUrl}"
        connection "scm:git:${gitUrl}"
        developerConnection "scm:git:${gitUrl}"
    }
    licenses {
        license {
            name 'Apache License 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }
    developers {
        developer {
            name 'David Nault'
            email 'dnault@mac.com'
            organization 'dnault'
            organizationUrl 'https://github.com/dnault'
        }
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().appendNode('description', projectDescription)
                asNode().children().last() + pomConfig
            }

            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

task install {
    dependsOn(publishToMavenLocal)
}
tasks.withType(JavaCompile) {
    // retain parameter names in class files
    options.compilerArgs << "-parameters"
}

checkstyle {
    toolVersion = "6.16.1"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

asciidoctor {
    attributes 'source-highlighter': 'coderay',
           // toc: '',
            idprefix: '',
            idseparator: '-',
            'data-uri': '',
            example: "${projectDir}/examples/src/main/java/com/github/therapi/example/"

    // icons               : '',
    // 'data-uri'          : ''
}

bintrayUpload.doFirst {
    if (version.contains("SNAPSHOT")) {
        throw new RuntimeException("Must not upload snapshots to bintray (current version is ${version}) -- create and tag a release version first!")
    }
}

bintray {
    if (project.hasProperty('bintrayUsername')) {
        user = project.bintrayUsername
    }

    if (project.hasProperty('bintrayApiKey')) {
        key = project.bintrayApiKey
    }

    publications = ['mavenJava'] // When uploading Maven-based publication files

    dryRun = false  //false //Whether to run this as dry-run, without deploying
    publish = false //true //If version should be auto published after an upload

    pkg {
        repo = 'maven'
        //  userOrg = 'myorg' //An optional organization name when the repo belongs to one of the user's orgs
        name = 'therapi-json-rpc'
        desc = 'JSON-RPC 2.0 framework for Java'
        websiteUrl = "https://github.com/dnault/${githubProjectName}"
        issueTrackerUrl = "https://github.com/dnault/${githubProjectName}/issues"
        vcsUrl = "https://github.com/dnault/${githubProjectName}.git"
        licenses = ['Apache-2.0']
        labels = ['json-rpc']
        publicDownloadNumbers = false//true
        //attributes= ['a': ['ay1', 'ay2'], 'b': ['bee'], c: 'cee'] //Optional package-level attributes
        //Optional version descriptor
        version {
            name = project.version //'1.3-Final' //Bintray logical version name
            //desc = 'optional, version-specific description'
            released = new Date() //'optional, date of the version release' //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
            vcsTag = "v" + project.version //'1.3.0'
//            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin'] //Optional version-level attributes
            gpg {
                //              sign = true //Determines whether to GPG sign the files. The default is false
                //            passphrase = 'passphrase' //Optional. The passphrase for GPG signing'
            }
            mavenCentralSync {
                sync = false //true //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = 'userToken' //OSS user token
                password = 'paasword' //OSS user password
                close = '0'//'1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}